.data
inputstr: .asciiz "Scrivi il numero: "

.text
.globl main
main:
	
	li $v0, 4           # input del numero
	la $a0, inputstr
	syscall
	li $v0, 5
	syscall

	move $a0, $v0    # parametro in $a0
	jal fattoriale   # calcola fattoriale, risultato in $v0

	move $a0, $v0
	li $v0, 1        # scrivi il risultato ed esci
	syscall
	li $v0, 10
	syscall

fattoriale:
	bnez $a0, ricorsione
	li $v0, 1               # caso base
	jr $ra
ricorsione:
	addi $sp,$sp, -8            # fai spazio sullo stack
	sw $a0, 4($sp)          # salva il parametro corrente
	sw $ra, 0($sp)          # e l'indirizzo di ritorno
	
	addi $a0,$a0, -1            # decrementa il parametro
	jal fattoriale          # chiamata ricorsiva
	lw $a0, 4($sp)          # ripristina il parametro dallo stack
	mul $v0, $a0, $v0       # moltiplicalo per il risultato della chiamata ricorsiva
	lw $ra, 0($sp)          # ripristina $ra
	addi $sp,$sp,8             # dealloca lo spazio sullo stack
	jr $ra                  # ritorna al chiamante